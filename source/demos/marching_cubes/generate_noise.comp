#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_GOOGLE_include_directive : enable

#include "noise.glsl"

#define POSITION_SCALE 32.0f // Value makes it look OK

layout (set = 0, binding = 0, r8) uniform writeonly image3D sdfImage;

float sdBox(vec3 p, vec3 b)
{
   vec3 d = abs(p) - b;
   return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
}

float density(vec3 pos)
{
   float density = -1; //80.0f - pos.y;

   vec3 scaledPos = pos / POSITION_SCALE;
   float noise = fbm(scaledPos) + 0.6 - 0.4 * scaledPos.y; // Generally increase solid volumes but decrease it by Y coordinate
   density = max(noise, density);

   vec3 side = imageSize(sdfImage);
   float boundingBox = sdBox(side / 2 - pos, side / 2 - 1.0f);
   density = min(-boundingBox, density);

   return density;
}

void main(void)
{
   float density = density(gl_GlobalInvocationID.xyz / 1.0f);
   imageStore(sdfImage, ivec3(gl_GlobalInvocationID.xyz), vec4(density));
}